allprojects {
    apply plugin: 'maven'
    apply plugin: 'groovy'
    apply plugin: 'maven'
    apply plugin: 'signing'

    group = 'org.fix4j'
    version = '1.6'
}

def deployUsername = hasProperty('ossrhUsername') ? ossrhUsername : 'not-given'
def deployPassword = hasProperty('ossrhPassword') ? ossrhPassword : 'not-given'

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

subprojects {
    apply plugin: 'java'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    repositories {
        mavenLocal()

        maven { url "https://repo.maven.apache.org/maven2" }
        maven { url "http://repo.marketcetera.org/maven" }
    }

    dependencies {
        compile group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.3.0'
        compile group: 'org.slf4j', name: 'slf4j-api', version: '1.3.0'
        testCompile group: 'junit', name: 'junit', version: '4.11'
        testCompile "org.spockframework:spock-core:1.0-groovy-2.4"
        // dependencies for using Spock
        testCompile "org.hamcrest:hamcrest-core:1.3" // only necessary if Hamcrest matchers are used
        testRuntime "cglib:cglib-nodep:3.1"          // allows mocking of classes (in addition to interfaces)
        testRuntime "org.objenesis:objenesis:2.1"    // allows mocking of classes without default constructor (together with CGLIB)

    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    signing {
        required { gradle.taskGraph.hasTask("uploadArchives") }
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: deployUsername, password: deployPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: deployUsername, password: deployPassword)
                }

                pom.project {
                    name 'Fix4j Assert'
                    packaging 'jar'
                    artifactId project.name
                    description 'fix4j-assert is a library to assist in testing of applications using FIX protocol'
                    url 'http://www.fix4j.org'

                    scm {
                        connection 'scm:git:git://github.com/tools4j/groovy-tables.git'
                        developerConnection 'scm:git:git@github.com:tools4j/fix4j-assert.git'
                        url 'https://github.com/tools4j/fix4j-assert'
                    }

                    licenses {
                        license {
                            name 'GNU General Public License (GPL)'
                            url 'http://www.gnu.org/licenses/gpl.txt'
                        }
                    }

                    developers {
                        developer {
                            id 'ben'
                            name 'Ben Warner'
                            email 'bjwarner@gmail.com'
                        }
                    }
                }
            }
        }
    }
}

